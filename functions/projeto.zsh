#!/bin/zsh

# Proposito: Abrir diretorio no Antigravity (file manager)
# Uso: levitar [caminho]
levitar() {
    local alvo="${1:-.}"

    if ! command -v antigravity &> /dev/null; then
        __err "'antigravity' nao encontrado no PATH."
        return 1
    fi

    echo -e "  ${D_COMMENT}Abrindo no Antigravity...${D_RESET}"
    nohup antigravity "$alvo" > /dev/null 2>&1 &
}

# Proposito: Setup completo de projeto (cd, branch, venv, deps, git context)
# Uso: santuario <Projeto> [Branch] [--sync] [--portfolio]
santuario() {
    local projeto_raiz="$1"

    if [ -z "$projeto_raiz" ]; then
        __header "SANTUARIO" "$D_PURPLE"
        echo -e "  ${D_COMMENT}Uso: santuario <Projeto> [Sub/Branch] [--sync] [--portfolio]${D_RESET}"
        echo ""
        echo -e "  ${D_FG}Exemplos:${D_RESET}"
        echo -e "    ${D_GREEN}santuario Luna${D_RESET}                 Abre projeto Luna"
        echo -e "    ${D_GREEN}santuario Luna dev${D_RESET}             Abre e muda para branch dev"
        echo -e "    ${D_GREEN}santuario Luna --sync${D_RESET}          Abre e sincroniza deps"
        echo -e "    ${D_GREEN}santuario repo --portfolio${D_RESET}     Abre projeto em Portfolio/"
        echo ""
        return 1
    fi
    shift

    local sync_dependencias=false
    local perfil_portfolio=false
    local alvo_primario=""
    local alvo_secundario=""

    while (( $# > 0 )); do
        case "$1" in
            --sync|-s) sync_dependencias=true ;;
            --portfolio|--port|-p) perfil_portfolio=true ;;
            --*) local limpo="${1#--}";
                 if [ -z "$alvo_primario" ]; then alvo_primario="$limpo"; else alvo_secundario="$limpo"; fi ;;
            *)   if [ -z "$alvo_primario" ]; then alvo_primario="$1"; else alvo_secundario="$1"; fi ;;
        esac
        shift
    done

    local base_dir="${DEV_DIR:-$HOME/Desenvolvimento}"
    if [ "$perfil_portfolio" = true ]; then
        base_dir="$base_dir/${WS_PORTFOLIO_DIR:-Portfolio}"
    fi
    local dir_alvo="$base_dir/$projeto_raiz"
    local branch_alvo=""

    if [ -n "$alvo_primario" ]; then
        if [ -d "$dir_alvo/$alvo_primario" ]; then
            dir_alvo="$dir_alvo/$alvo_primario"
            [ -n "$alvo_secundario" ] && branch_alvo="$alvo_secundario"
        else
            branch_alvo="$alvo_primario"
        fi
    fi

    if [ ! -d "$dir_alvo" ]; then
        __err "Caminho nao existe: $dir_alvo"
        return 1
    fi

    cd "$dir_alvo" || return

    __header "SANTUARIO: $(basename "$dir_alvo")" "$D_PURPLE"
    __item "Path" "$(pwd)" "$D_COMMENT" "$D_FG"

    if [ -n "$branch_alvo" ]; then
        if [ -d ".git" ] || [ -d "../.git" ]; then
            echo ""
            echo -e "  ${D_COMMENT}Trocando para branch:${D_RESET} ${D_YELLOW}$branch_alvo${D_RESET}"

            if git show-ref --verify --quiet "refs/heads/$branch_alvo"; then
                git checkout "$branch_alvo"
            else
                git fetch origin "$branch_alvo" >/dev/null 2>&1
                if git show-ref --verify --quiet "refs/remotes/origin/$branch_alvo"; then
                    git checkout "$branch_alvo"
                else
                    __warn "Branch '$branch_alvo' nao encontrada."
                    echo -e -n "  ${D_FG}Criar nova branch? (s/N)${D_RESET} "
                    read -k 1 reply
                    echo ""
                    if [[ "$reply" == "s" || "$reply" == "S" ]]; then
                        git checkout -b "$branch_alvo"
                    else
                        echo -e "  ${D_COMMENT}Mantendo branch atual.${D_RESET}"
                    fi
                fi
            fi
        fi
    fi

    echo ""

    if [[ "$(pwd)" == *"/${WS_PORTFOLIO_DIR:-Portfolio}/"* ]]; then
        echo -e "  ${D_PURPLE}Protocolo Portfolio${D_RESET}"
        __aplicar_contexto_git_automatico
    else
        if [ -f "Cargo.toml" ]; then
            echo -e "  ${D_COMMENT}Projeto Rust detectado. Compilando...${D_RESET}"
            cargo build
        else
            local req_files=($(find . -maxdepth 1 -name "requirements*.txt"))

            if [ ${#req_files[@]} -eq 0 ]; then
                echo -e "  ${D_COMMENT}Nenhum requirements.txt encontrado.${D_RESET}"
            else
                echo -e "  ${D_CYAN}${#req_files[@]} requirements detectado(s)${D_RESET}"

                for req in "${req_files[@]}"; do
                    local req_nome=$(basename "$req")
                    local venv_target=""

                    if [[ "$req_nome" == "requirements.txt" ]]; then
                        venv_target="venv"
                    else
                        local sufixo=${req_nome#requirements_}
                        sufixo=${sufixo%.txt}
                        venv_target="venv_${sufixo}"
                    fi

                    if [ ! -d "$venv_target" ]; then
                        echo -e "  ${D_GREEN}[NOVO]${D_RESET} Criando '$venv_target' ($req_nome)..."
                        python3 -m venv "$venv_target"
                        source "$venv_target/bin/activate"
                        pip install -r "$req_nome"
                        deactivate
                    elif [ "$sync_dependencias" = true ]; then
                        echo -e "  ${D_ORANGE}[SYNC]${D_RESET} Atualizando '$venv_target' ($req_nome)..."
                        source "$venv_target/bin/activate"
                        pip install -r "$req_nome"
                        deactivate
                    fi
                done
            fi

            local venvs=($(find . -maxdepth 1 -type d \( -name "venv*" -o -name ".venv*" \) -printf "%f\n" | sort))
            local venv_escolhido=""

            if [ ${#venvs[@]} -eq 1 ]; then
                venv_escolhido="${venvs[1]}"
            elif [ ${#venvs[@]} -gt 1 ]; then
                __verificar_dependencias "fzf" || return 1
                local FZF_DRACULA="--color=bg+:#44475a,fg+:#f8f8f2,hl:#bd93f9,hl+:#ff79c6,pointer:#50fa7b,marker:#50fa7b,prompt:#bd93f9,header:#6272a4,border:#6272a4"
                echo ""
                venv_escolhido=$(printf "%s\n" "${venvs[@]}" | \
                    fzf --height=20% --layout=reverse --border \
                    --prompt="  Ambiente > " $FZF_DRACULA)
            fi

            if [ -n "$venv_escolhido" ]; then
                if [ -f "$venv_escolhido/bin/activate" ]; then
                    source "$venv_escolhido/bin/activate"
                    __item "Venv" "$venv_escolhido" "$D_COMMENT" "$D_GREEN"
                else
                    __err "Ambiente '$venv_escolhido' corrompido."
                fi
            fi
        fi
    fi

    if [ -f ".santuario_setup.sh" ]; then
        echo -e "  ${D_COMMENT}Executando .santuario_setup.sh...${D_RESET}"
        source ./.santuario_setup.sh
    fi

    __aplicar_contexto_git_automatico

    echo ""
    __ok "Santuario pronto."

    if command -v git_info &> /dev/null; then git_info; fi

    read -k 1 "reply?  Abrir no Antigravity? (s/N) "
    echo ""
    if [[ "$reply" == "s" || "$reply" == "S" ]]; then
        levitar .
    fi
}

# "A ordem e a primeira lei do ceu." -- Alexander Pope
